title Ever On
run_rules_on_level_start
youtube Tbz9EYtzDEo

========
OBJECTS
========

Tick
Black
.....
.....
.....
.....
.....

Tock
Black
.....
.....
.....
.....
.....

Out
Black

In
Black


Player1
DarkRed Blue Grey DarkBrown 
.333.
.321.
.322.
.300.
..20.

Player2
DarkRed Blue Grey DarkBrown 
.333.
.321.
.322.
.300.
..02.

Ground
Brown


Seeding
Red
0....
.....
.....
.....
.....

TopGround
Green Brown
00000
11101
11111
11111
11111

InSeed
Green

Moon
Yellow
.0...
0....
0....
00..0
.000.

=======
LEGEND
=======
ForeGround = TopGround or Ground
. = Tick
Background = Tick or Tock
Player = Player1 or Player2
P = Player1 and Seeding and Tick
M = Moon and Tick
O = Out and Tick
I = In and Tick

=======
SOUNDS
=======

================
COLLISIONLAYERS
================
Background
In, Out, Inseed, Moon
ForeGround
seeding
Player


======
RULES
======
[foreground out] -> [out]

(tick/tock)
[Tick] -> [Action Tick]
[Tock] -> [Action Tock]

[Action Tick] -> [Tock]
[Action Tock] -> [Tick]

[Player1 Tick] -> [Player2 Tick]
[Player2 Tock] -> [Player1 Tock]

(seeding)
down [Player|No TopGround][seeding] -> [Player|action TopGround][]

startloop
right [action TopGround|] -> [TopGround|randomdir TopGround]
up [up TopGround|||||] -> [|action TopGround||||]
down [down TopGround|] -> [|action TopGround]
[> TopGround] -> [action TopGround]
endloop

(player movement)
[stationary player] -> [right player] again

[up player] -> cancel
[left player] -> cancel
[down player] -> cancel

[player] -> [stationary player]

(scrolling and ground generation/destruction)
[foreground] -> [left foreground]

right [< topground stationary in] -> [< topground randomdir in]

up [up in|||||] -> [stationary in|InSeed||||]
up [up in] -> [stationary Inseed]

[right in] -> [stationary Inseed]
[left in] -> [stationary Inseed]

down [down in|] -> [stationary in|Inseed]
down [down in] -> [stationary Inseed]

[in] -> [stationary in]

(destruction/generation)
late [InSeed] -> [In TopGround]

(extend ground down)
late down [ForeGround|No Ground] -> [ForeGround|Ground]

(move player up)
late up [player TopGround|] -> [TopGround|player]
(move player down)
late down [player|no ForeGround] -> [|player]

==============
WINCONDITIONS
==============

=======
LEVELS
=======

O.M............I
O..............I
O..............I
OP.............I
O..............I
O..............I
O..............I
O..............I
O..............I