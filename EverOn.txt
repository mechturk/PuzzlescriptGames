title Ever On
run_rules_on_level_start
youtube Tbz9EYtzDEo

========
OBJECTS
========

Tick
Black
.....
.....
.....
.....
.....

Tock
Black
.....
.....
.....
.....
.....

Out
Black

In
Black


Player1
DarkRed Blue Grey DarkGrey
.333.
.321.
.322.
.300.
..20.

Player2
DarkRed Blue Grey DarkGrey
.333.
.321.
.322.
.300.
..02.

Blink3
Grey Blue DarkGrey DarkRed
22222
21012
20002
23..2
.3...

Blink1
Grey Blue DarkGrey DarkRed
22222
22222
22222
22222
.3...


Blink2
Grey Blue DarkGrey
.....
...0.
.....
.....
.....


Ground
Brown


Seeding
Red
0....
.....
.....
.....
.....

TopGround
Green Brown
00000
11101
11111
11111
11111

InSeed
Green

Moon
Yellow
.0...
0....
0....
00..0
.000.

BGTop
DarkGreen DarkBrown
.....
00000
10111
11111
11111

BGGround
DarkBrown

=======
LEGEND
=======
ForeGround = TopGround or Ground
BG = BGTop or BGGround
Blink = Blink1 or Blink2 or Blink3
. = Tick
Background = Tick or Tock
Player = Player1 or Player2
P = Player1 and Tick
M = Moon and Tick and Seeding
O = Out and Tick
I = In and Tick


Top = TopGround or BGTop
mn = in or inseed

=======
SOUNDS
=======

================
COLLISIONLAYERS
================
Background
In, Out, Inseed, Moon
BG
Player, Seeding
ForeGround, Blink




======
RULES
======
[foreground out] -> [out]
[bg out tock] -> [out tock]
[blink2] -> []

(tick/tock)
[Tick] -> [Action Tick]
[Tock] -> [Action Tock]

[Action Tick] -> [Tock]
[Action Tock] -> [Tick]

[Player1 Tick] -> [Player2 Tick]
[Player2 Tock] -> [Player1 Tock]

(seeding)
down [Player|][seeding] -> [Player|action TopGround action BGTop][]

startloop
right [action Top|] -> [Top|randomdir Top]
up [up Top|||||] -> [|action Top||||]
down [down Top|] -> [|action Top]
[> Top] -> [action Top]
endloop

(player movement)
[stationary player] -> [randomdir player] again

[up player] -> [player random blink]
([left player] -> [right player]
[down player] -> [right player])


[player] -> [stationary player]

(scrolling and ground generation/destruction)
[foreground] -> [left foreground]
[BG][Tick] -> [left BG][Tick]
right [blink| < foreground] -> [< blink| < foreground]

right [< topground stationary in] -> [< topground randomdir in]

up [up in|||||] -> [stationary in|InSeed||||]
up [up in] -> [stationary Inseed]

[right in] -> [stationary Inseed]
[left in] -> [stationary Inseed]

down [down in|] -> [stationary in|Inseed]
down [down in] -> [stationary Inseed]

[in] -> [stationary in]

right [< BGTop stationary mn] -> [< BGTop randomdir mn]

up [up mn|||||] -> [stationary mn|seeding||||]
up [up mn] -> [stationary mn seeding]

[right mn] -> [stationary mn seeding]
[left mn] -> [stationary mn seeding]

down [down mn|] -> [stationary mn|seeding]
down [down mn] -> [stationary mn seeding]

[mn] -> [stationary mn]

(destruction/generation)
late [InSeed] -> [In TopGround]
late [seeding] -> [BGTop]

(extend ground down)
late down [ForeGround|No Ground] -> [ForeGround|Ground]
late down [BG|No BG] -> [BG|BGGround]

(move player up)
late up [player TopGround|] -> [TopGround|player]
(move player down)
late down [player|no ForeGround] -> [|player]
late [blink no player] -> []

==============
WINCONDITIONS
==============

=======
LEVELS
=======

O.M............I
O..............I
O..............I
OP.............I
O..............I
O..............I
O..............I
O..............I
O..............I